<SQLStoredProcedureSpec
	xmlns="bb_appfx_sqlstoredprocedure"
	xmlns:common="bb_appfx_commontypes" 
	ID="045530cf-0c4b-4383-aaca-e46cb8c4886f"
	Name="USR_USP_SPONSORSHIPOPPORTUNITY_DEPARTURETRANSFER"
	Description="Completes departure transfer process"
	Author="Cary Mayeda"
	SPName="USR_USP_SPONSORSHIPOPPORTUNITY_DEPARTURETRANSFER" 
	GrantServiceRolePermission="true" >

	<!-- 
	Remarks:    Used to create departure call interaction on the financial sponsor's record

	History:
	Date            Modified By     Comments
	03-Sep-2012		CMayeda			Initial Version
	************  NEED TO FINISH  **************
	Departure reason and Departure other reason (waiting on questions to be answered)
	Set EFT gift to held
	-->

	<CreateProcedureSQL>
		<![CDATA[
create procedure dbo.USR_USP_SPONSORSHIPOPPORTUNITY_DEPARTURETRANSFER (
	@sponsorshipOpportunityChildID uniqueidentifier,	-- Sponsorship Opportunity Child ID (same as Sponsorship Opportunity ID) to process the departure transfer for
	@changeAgentID uniqueidentifier						-- The ChangeAgentID to be used for AddedBy and ModifiedBy fields
)
as begin
    if @changeAgentID is null  
		exec dbo.USP_CHANGEAGENT_GETORCREATECHANGEAGENT @changeAgentID output;

	-- Constants
	declare @callCenterManagerConstituentID uniqueidentifier = 'B25168DC-4DD4-46ED-B4DD-C5A26A965667'

	declare @interactionCategoryDesc nvarchar(100) = 'Sponsorship Transfers'
	declare @interactionSubcategoryDesc_DepartureNotification nvarchar(100) = 'Departure Notification'
	declare @interactionSubcategoryDesc_DeathOfChild nvarchar(100) = 'Death of Child Notification'
	
	declare @departureTypeCode_Departure tinyint = 0
	declare @departureTypeCode_Completion tinyint = 1
	declare @departureTypeCode_Administrative tinyint = 2
	
	declare @interactionResponseCategory_DonorContact nvarchar(100) = 'Donor Contact'
	declare @interactionResponseCategory_LeftMessage nvarchar(100) = 'Left Message'
	declare @interactionResponseCategory_MessageType nvarchar(100) = 'Message Type'
	declare @interactionResponseCategory_AcceptTransfer nvarchar(100) = 'Accept Transfer'
	
	declare @interactionResponse_No nvarchar(100) = 'No'
	declare @interactionResponse_None nvarchar(100) = 'None'
	declare @interactionResponse_Unknown nvarchar(100) = 'Unknown'
	
	
	declare @financialSponsorID uniqueidentifier = null
		
	declare @sponsorshipID uniqueidentifier 

	declare @interactionSummary nvarchar(100) = ''
	declare @interactionSubcategoryDesc nvarchar(100) = ''
	
	declare @departureTypeCode tinyint = null
	declare @departureDeath bit = 0
	declare @departureAdminCodeID uniqueidentifier = null
	declare @departureOther bit = 0
	--declare @departureOtherReason nvarchar(250)
	
	begin try
		set @financialSponsorID = dbo.USR_UFN_SPONSORSHIP_GETFINANCIALSPONSOR_BYSPONSORSHIPOPP (@sponsorshipOpportunityChildID)
		if @financialSponsorID is null
			raiserror 10000 'Could not find Financial Sponsor. No interactions were created, and the recurring gift was not processed (held if EFT).'

		select	@departureTypeCode = DEPARTURETYPECODE,
				@departureDeath = DEPARTURE_DEATHOFCHILD,
				@departureAdminCodeID = ADMINISTRATIVECODEID, 
				@departureOther = case 
									when isnull (DEPARTURE_OTHER, '') = '' then 0
								    else 1
								  end
				--@departureOtherReason = DEPARTURE_OTHER,
		  from	dbo.USR_CHILDDEPARTURE
		 where	ISCURRENTDEPARTURECODE = 1
		   and	SPONSORSHIPOPPORTUNITYCHILDID = @sponsorshipOpportunityChildID

		if 	@departureDeath = 1
		begin
			set @interactionSummary = 'Death of child'
			set @interactionSubcategoryDesc = @interactionSubcategoryDesc_DeathOfChild
		end
		
		else
		begin
			set @interactionSummary =	case 
											when @departureTypeCode = @departureTypeCode_Completion							then 'Child completed program'
											when @departureTypeCode = @departureTypeCode_Administrative						then (select DESCRIPTION from dbo.USR_DEPARTURE_ADMINISTRATIVE_CODE where ID = @departureAdminCodeID)
											when @departureTypeCode = @departureTypeCode_Departure and @departureOther = 1	then 'Other reason child is ineligible'  --??
											when @departureTypeCode = @departureTypeCode_Departure and @departureOther = 0	then 'Child departure'  -- Need to add specific reason
											else 'unknown departure type'
										end

			if @interactionSummary is null
				set @interactionSummary = 'Unknown Administrative code'

			set @interactionSubcategoryDesc = @interactionSubcategoryDesc_DepartureNotification 

		end
		
		declare @interactionID uniqueidentifier = null
		declare @currentDate date = getdate()
		exec dbo.USR_USP_INTERACTION_SPONSOR_ADD_PENDING 
			@id = @interactionID output,
			@constituentID = @financialSponsorID,
			@summary = @interactionSummary,
			@categoryName = @interactionCategoryDesc, 
			@subcategoryName = @interactionSubcategoryDesc,
			@expectedDate = @currentDate,
			@owner = @callCenterManagerConstituentID,
			@contactMethodDesc = 'Phone',
			@comment = '',						
			@letterTypeDesc = '',
			@fulfillmentStatusDesc = '', 
			@eftBrochureCode = 0, 
			@resendCode = 0,
			@changeAgentID = @changeAgentID				

		if @interactionID is not null
		begin
			exec dbo.USR_USP_INTERACTION_SPONSOR_SETDEPARTEDCHILD
				@interactionID = @interactionID,
				@sponsorshipOpportunityChildID = @sponsorshipOpportunityChildID,
				@changeAgentID = @changeAgentID

			exec dbo.USR_USP_INTERACTION_ADDRESPONSE 
				@interactionID = @interactionID,
				@responseCategoryName = @interactionResponseCategory_DonorContact,
				@response = @interactionResponse_No,
				@responseDate = null,		
				@changeAgentID = @changeAgentID

			exec dbo.USR_USP_INTERACTION_ADDRESPONSE 
				@interactionID = @interactionID,
				@responseCategoryName = @interactionResponseCategory_LeftMessage,
				@response = @interactionResponse_No,
				@responseDate = null,		
				@changeAgentID = @changeAgentID

			exec dbo.USR_USP_INTERACTION_ADDRESPONSE 
				@interactionID = @interactionID,
				@responseCategoryName = @interactionResponseCategory_MessageType,
				@response = @interactionResponse_None,
				@responseDate = null,		
				@changeAgentID = @changeAgentID
				
			exec dbo.USR_USP_INTERACTION_ADDRESPONSE 
				@interactionID = @interactionID,
				@responseCategoryName = @interactionResponseCategory_AcceptTransfer,
				@response = @interactionResponse_Unknown,
				@responseDate = null,		
				@changeAgentID = @changeAgentID
	
		end

	end try
	begin catch
	    exec dbo.USP_RAISE_ERROR
		return 1	
	end catch		  
	
	
end

		]]>
	</CreateProcedureSQL>

</SQLStoredProcedureSpec>
